buildscript {
    ext {
        grailsVersion = project.grailsVersion
    }
    repositories {
        mavenLocal()
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://repo.grails.org/grails/core" }
        maven { url "http://repo.spring.io/milestone/" }
        maven { url "https://repo.grails.org/grails/plugins" }
        mavenCentral()
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "org.grails.plugins:views-gradle:1.2.0"
    }
}

group "com.causecode.plugins"
version "2.6.2"

apply plugin: "idea"
apply plugin: "org.grails.grails-plugin"
apply plugin: 'org.grails.plugins.views-json'
apply plugin: "org.grails.grails-gsp"
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'signing'

ext {
    grailsVersion = project.grailsVersion
    gradleWrapperVersion = project.gradleWrapperVersion
}

repositories {
    mavenLocal()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "http://repo.spring.io/milestone/" }
    maven { url "https://repo.grails.org/grails/plugins" }
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    // Default dependencies in grails 3.2.0 for plugin
    // While porting to a new grails version, create a blank app and replace the default dependencies with the new ones.
    compile "org.springframework.boot:spring-boot-starter-logging:1.4.0.RELEASE"
    compile "org.springframework.boot:spring-boot-autoconfigure:1.4.0.RELEASE"
    compile "org.grails:grails-core:3.2.0"
    compile "org.springframework.boot:spring-boot-starter-actuator:1.4.0.RELEASE"
    compile "org.springframework.boot:spring-boot-starter-tomcat:1.4.0.RELEASE"
    compile "org.grails:grails-dependencies:3.2.0"
    compile "org.grails:grails-web-boot:3.2.0"
    compile "org.grails.plugins:cache:3.0.3"
    compile "org.grails.plugins:scaffolding:3.2.1"
    compile "org.hibernate:hibernate-ehcache:5.0.9.Final"
    compile "cglib:cglib-nodep:2.2.2"

    provided "org.codehaus.groovy:groovy-ant:2.4.7"
    provided "com.h2database:h2:1.4.192"

    testCompile "org.grails:grails-plugin-testing:3.2.0"

    /*
    * Dependencies which were needed for the application/plugin.
    */
    compile "org.grails.plugins:hibernate5:6.0.0"
    compile "org.grails.plugins:spring-security-core:3.1.1"
    compile "org.ow2.asm:asm:5.0.3"
    compile "org.grails.plugins:grails-markdown:3.0.0"
    provided "com.causecode.plugins:file-uploader:3.0.11"
    provided "org.grails.plugins:quartz:2.0.8"
    provided "com.causecode.plugins:nucleus:0.4.81"
    compile ("com.causecode.plugins:taggable:1.4.1") {
        exclude module: "hibernate"
    }

    profile "org.grails.profiles:web-plugin:3.1.4"

    provided "org.grails:grails-plugin-services:3.2.0"
    provided "org.grails:grails-plugin-domain-class:3.2.0"

    // Grails JSON Views
    provided "org.grails.plugins:views-json:1.1.0"
    provided "org.grails.plugins:views-json-templates:1.1.0"

    testCompile "org.grails.plugins:build-test-data:3.0.1"
}

artifacts {
    archives jar

    archives javadocJar
    archives sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {

            if (!project.hasProperty('ossrhUsername') || !project.hasProperty('ossrhPassword')) {
                return
            }

            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'content'
                packaging 'jar'
                description '''A plugin used to manage contents like static pages, menus etc. at one place.
                        Also provides shortened and user friendly urls.'''
                url 'https://github.com/causecode/static-content'

                scm {
                    url 'scm:svn:https://github.com/causecode/static-content'
                    connection 'scm:svn:https://github.com/causecode/static-content.git'
                    developerConnection 'https://github.com/causecode/static-content.git'
                }

                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution 'repo'
                    }
                }

                developers {
                    developer {
                        id 'causecode'
                        name 'CauseCode Technologies'
                        email 'bootstrap@causecode.com'
                    }
                }
            }
        }
    }
}

// Adding migration files to jar
sourceSets {
    main {
        resources {
            srcDir 'grails-app/migrations'
        }
    }
}
